in ../../../match-imp

mod TEST is inc IMP + DOUBLE-LINKED-LIST .

  ops a x l : -> PVar .
  ops ?x ?l ?y : -> ?Int .
  ops a0 b0 : -> FreeInt .
  ops ?A ?A' ?X : -> ?IntSeq .
  op A : -> FreeIntSeq .
  
  op pDListLength : -> K .
  eq pDListLength = (
    assume <config> <env> a |-> a0 ;; x |-> ?x ;; l |-> ?l </env>
                    <heap> dlseg(a0,b0)(0,0,A) </heap> <form> TrueFormula </form> </config> ;
    x = a ;
    l = 0 ;
    invariant <config> <env> a |-> a0 ;; x |-> ?x ;; l |-> ?l </env>
                       <heap> dlseg(a0,?y)(?x,0,?A) ** dlseg(?x,b0)(0,?y,?X) </heap>
                       <form> ?A :: ?X === A /\ ?l === len(?A) </form> </config> ;
    while (x != 0) {
        x = *(x + 1) ;
        l = l + 1 ;
    }
    assert <config> <env> a |-> a0 ;; x |-> ?x ;; l |-> ?l </env>
                    <heap> dlseg(a0,b0)(0,0,A) </heap> <form> ?l === len(A) </form> </config> ;
  ) .
endm

rew [| pDListLength |] .