***(
    This file is part of the Matching Logic Verification Framework

    Copyright (C) 2009-2011 Grigore Rosu

    This file is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307, USA.
***)


mod MATHEMATICAL-DOMAIN-BUILTIN-MODULE is
  inc MATHEMATICAL-OBJECT-BUILTIN-MODULE
    + MATHEMATICAL-INT-BUILTIN-MODULE
    + MATHEMATICAL-SEQUENCE-BUILTIN-MODULE
    + MATHEMATICAL-SET-BUILTIN-MODULE
    + MATHEMATICAL-MULTISET-BUILTIN-MODULE
    + MATHEMATICAL-TREE-BUILTIN-MODULE
    + MATHEMATICAL-GRAPH-BUILTIN-MODULE
    + MATHEMATICAL-SCHORR-WAITE-BUILTIN-MODULE .

  op seq2mset : Seq++ -> NeMSet++ .
  op tree2list : Tree++ -> NeSeq++ .
  op tree2preorder : Tree++ -> NeSeq++ .
  op tree2postorder : Tree++ -> NeSeq++ .
  op tree2mset : Tree++ -> NeMSet++ .
  op treeList2list : Seq++ -> Seq++ .


  op isSorted : Seq++ -> AtomFormula .
  op isBst : Tree++ -> AtomFormula .
  op isHeightTree : Tree++ -> AtomFormula .
  op isBalanced : Tree++ -> AtomFormula .
  op isAvl : Tree++ -> AtomFormula .


  var Phi Phi1 Phi2 : Formula .
  var Obj Obj1 Obj2 : MathObj++ .
  var I J : Int++ .
  var A A1 A2 : Seq++ .
  var NeA NeA1 NeA2 : NeSeq++ .
  var M M1 M2 : MSet++ .
  var T TL TR : Tree++ .


  eq tree2list(TL [ Obj ] TR) = tree2list(TL) @ [Obj] @ tree2list(TR) .
  eq tree2list(upsilon) = epsilon .

  eq tree2preorder(TL [ Obj ] TR)
   = [Obj] @ tree2preorder(TL) @ tree2preorder(TR) .
  eq tree2preorder(upsilon) = epsilon .

  eq tree2postorder(TL [ Obj ] TR)
   = tree2postorder(TL) @ tree2postorder(TR) @ [Obj] .
  eq tree2postorder(upsilon) = epsilon .

  eq tree2mset(TL [ Obj ] TR) = tree2mset(TL) U {| Obj |} U tree2mset(TR) .
  eq tree2mset(upsilon) = empty .

  eq seq2mset(NeA1 @ NeA2) = seq2mset(NeA1) U seq2mset(NeA2) .
  eq seq2mset([Obj]) = {| Obj |} .
  eq seq2mset(epsilon) = empty .

  eq treeList2list(A1 @ [T] @ A2)
   = treeList2list(A1) @ tree2list(T) @ treeList2list(A2) .
  eq treeList2list(epsilon) = epsilon .


  eq isSorted(NeA1 @ NeA2)
   = leq(seq2mset(NeA1), seq2mset(NeA2))
     /\ isSorted(NeA1) /\ isSorted(NeA2) .
  eq isSorted([I]) = TrueFormula .
  eq isSorted(epsilon) = TrueFormula .

  eq isBst(TL [Obj] TR)
   = leq(tree2mset(TL), {| Obj |}) /\ leq({| Obj |}, tree2mset(TR))
     /\ isBst(TL) /\ isBst(TR) .
  eq isBst(upsilon) = TrueFormula .

  eq isHeightTree(TL [I] TR)
   = I === height(TL [I] TR) /\ isHeightTree(TL) /\ isHeightTree(TR) .
  eq isHeightTree(upsilon) = TrueFormula .

  eq isBalanced(TL [Obj] TR)
   = @(-1 <=Int (height(TL) -Int height(TR)))
     /\ @((height(TL) -Int height(TR)) <=Int 1)
     /\ isBalanced(TL) /\ isBalanced(TR) .
  eq isBalanced(upsilon) = TrueFormula .

  eq isAvl(T) = isBst(values(T)) /\ isHeightTree(heights(T)) /\ isBalanced(T) .


  eq seq2mset(A) === empty = A === epsilon .
  eq (A === epsilon \/ len(A) === 1)
     /\ seq2mset(A) === {| Obj1 |} U {| Obj2 |} U M
   = FalseFormula .


 ceq A1 @ [Obj] @ A2 === A /\ Phi1 ===> {| Obj |} U M1 === M /\ Phi2
   = A1 @ [Obj] @ A2 === A /\ seq2mset(A1 @ [Obj] @ A2) === seq2mset(A) /\ Phi1
     ===> {| Obj |} U M1 === M /\ Phi2
  if (A1 @ [Obj] @ A2 === A /\ seq2mset(A1 @ [Obj] @ A2) === seq2mset(A)
     /\ Phi1)
     =/=Bool
     (A1 @ [Obj] @ A2 === A /\ Phi1) .
 ceq A1 @ NeA @ A2 === A /\ Phi1 ===> seq2mset(NeA) U M1 === M /\ Phi2
   = A1 @ NeA @ A2 === A /\ seq2mset(A1 @ NeA @ A2) === seq2mset(A) /\ Phi1
     ===> seq2mset(NeA) U M1 === M /\ Phi2
  if (A1 @ NeA @ A2 === A /\ seq2mset(A1 @ NeA @ A2) === seq2mset(A)
     /\ Phi1)
     =/=Bool
     (A1 @ NeA @ A2 === A /\ Phi1) .


 ceq norm([Obj1] @ A1 === A2 /\ leq({| Obj1 |}, seq2mset(A1)) /\ isSorted(A1)
          /\ Phi)
   = norm([Obj1] @ A1 === A2 /\ leq({| Obj1 |}, seq2mset(A1)) /\ isSorted(A1)
          /\ isSorted(A2) /\ Phi)
  if ([Obj1] @ A1 === A2 /\ leq({| Obj1 |}, seq2mset(A1)) /\ isSorted(A1)
     /\ isSorted(A2) /\ Phi)
     =/=Bool
     ([Obj1] @ A1 === A2 /\ leq({| Obj1 |}, seq2mset(A1)) /\ isSorted(A1)
     /\ Phi) .


  eq @(1 +Int (-Int len(A)) >=Int 0) /\ Phi1 ===> isSorted(A) /\ Phi2
   = @(1 +Int (-Int len(A)) >=Int 0) /\ isSorted(A) /\ Phi1 ===> Phi2 .
endm

